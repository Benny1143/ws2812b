module au_top (
    input clk,              // 100MHz clock
    input rst_n,            // reset button (active low)
    output led [8],         // 8 user controllable LEDs
    input usb_rx,           // USB->Serial input
    output outled,           // to LED strip, Br pin C49, connect LED Strip to 5V for power
    output usb_tx,          // USB->Serial output
    output io_led [3][8],   // LEDs on IO Shield
    output io_seg [8],      // 7-segment LEDs on IO Shield
    output io_sel [4],      // Digit select on IO Shield
    input io_button [5],    // 5 buttons on IO Shield
    input io_dip [3][8]     // DIP switches on IO Shield
  ) {
  
  // We shall always assume that COLUMN_DIMENSION and ROW_DIMENSION is a 2^x
  const COLUMN_DIMENSION = 16; // how many "columns"? 
  const ROW_DIMENSION = 4; // how many "rows"?
  const COLUMN_DIMENSION_BITS = $clog2(COLUMN_DIMENSION); // number of bits needed to represent which column we are currently at

  
  const PIXEL_COUNT = COLUMN_DIMENSION *  ROW_DIMENSION;   
  const HIGHER_BITS = $clog2(PIXEL_COUNT) - COLUMN_DIMENSION_BITS;
  const ENCODING_AMOUNT = 4; // how many different encodings are used? As example, we use 4
  
  sig rst; // reset signal
  sig encoded_pixel_address[$clog2(PIXEL_COUNT)*$clog2(ENCODING_AMOUNT)];
  sig current_color_encoding[$clog2(ENCODING_AMOUNT)];
  sig reversed_pixel_address[$clog2(PIXEL_COUNT)]; // reverse every other row for ws2812b matrices

  var index;

  // WHITE (11), BLUE (10), RED (01), GREEN (00)
  const LEDCOLOR = {24hFFFFFF, 24hFF0000, 24h00FF00, 24h0000FF}; 
  
  // observed output of the strip: (if matrix is used, this is the value of the top row or first row)
  // (connector) WHITE WHITE BLUE WHITE RED WHITE GREEN WHITE BLUE BLUE BLUE BLUE BLUE BLUE BLUE BLUE (end of strip)
  // e.g: E is 1110 ---> 10 first (BLUE) then 11 (WHITE)
  // as example, we repeat pattern each row, assuming there are 16 cols in a row hence 32 bits of encoded data per row
  dff led_encoding[PIXEL_COUNT*2](.clk(clk), .rst(rst), #INIT(ROW_DIMENSIONx{32hAAAACDEF})); // 2 bit to encode a color per pixel
  dff temp_encoding[PIXEL_COUNT*2](.clk(clk), .rst(rst), #INIT(0)); // holds temporary value (double buffering)
  
  ws2812b_writer led_strip(#PIXEL_COUNT(PIXEL_COUNT), .clk(clk), .rst(rst)); 
  fsm state(.clk(clk), .rst(rst)) = {IDLE, LOAD};
  
  // helper bits to reverse every other row if we are using ws2812b matrix
  dff reverse(.clk(clk), .rst(rst), #INIT(0));
  
  // user inputs 
  button_conditioner latch_button(.clk(clk));
  edge_detector latch_button_edge(.clk(clk), #RISE(1), #FALL(0));
  button_conditioner clear_button(.clk(clk));
  edge_detector clear_button_edge(.clk(clk), #RISE(1), #FALL(0));
  button_conditioner reverse_button(.clk(clk));
  edge_detector reverse_button_edge(.clk(clk), #RISE(1), #FALL(0));
  dff matrix_used(#INIT(0), .clk(clk), .rst(rst));
  
  .clk(clk) {
    // The reset conditioner is used to synchronize the reset signal to the FPGA
    // clock. This ensures the entire FPGA comes out of reset at the same time.
    reset_conditioner reset_cond;
  }
  
  always {
    reset_cond.in = ~rst_n; // input raw inverted reset signal
    rst = reset_cond.out;   // conditioned reset
    
    usb_tx = usb_rx;        // loop serial port
    
    led = 8h00;             // turn LEDs off
    
    io_led = 3x{{8h00}};    // turn LEDs off
    io_seg = 8hff;          // turn segments off
    io_sel = 4hf;           // select no digits
    
    latch_button.in = io_button[0]; // use this button to latch io_dip value to led_encoding dff 
    latch_button_edge.in = latch_button.out;
    
    clear_button.in = io_button[2]; // use this button to turn off all LEDs 
    clear_button_edge.in = clear_button.out;
    
    reverse_button.in = io_button[1]; // use this button to enable reverse
    reverse_button_edge.in = reverse_button.out;
    
    // toggle matrix used button 
    if (reverse_button_edge.out){
      matrix_used.d = matrix_used.q + 1;
    }
    
    led_strip.update = 0; // do not refresh by default
    led_strip.clear = 0; // do not clear by default
    
    // EXAMPLE: 16x16 ws2812b matrix transformation logic
    // pixel 0 to 15 is not reversed, reverse.q == 0, current_row.q == 0
    // pixel 16 to 31 is reversed, becomes 31 to 16, reverse.q == 1, current_row.q == 1
    // 16 ---> 31
    // 17 ---> 30 
    // 31 ---> 16 
    // pixel 32 to 47 is not reversed, reverse.q == 0, current_row.q == 2
    // pixel 48 to 63 is reversed, reverse.q == 1, current_row.q == 3
    // 48 ---> 63 
    // 49 ---> 62 
    // and so on
    // we can do this by keeping the higher bit, and do a 1s complement of the lower COLUMN_DIMENSION bits (bitwise XOR with 1)
    
    // check if we are a multiple of DIMENSION 
    // here in our example, we have column 0 to 15
    // so each time the last four bit our pixel_address is 1111, we will reverse the row addressing
    // when address turns 1111, we can't just change the encoding right away, we need to wait for it to finish loading first, all 24 bits sent
    if  (&led_strip.pixel_address[COLUMN_DIMENSION_BITS-1:0] & led_strip.next_pixel){
      reverse.d = reverse.q + 1; // toggle the reverse flag, test 1
    }
    
    if (reverse.q)
    {
       reversed_pixel_address = led_strip.pixel_address ^ c{HIGHER_BITSx{b0},COLUMN_DIMENSION_BITSx{b1}}; // higher bits stays the same 
      // reversed_pixel_address = 0; // test color at 0 which is 11 --> white
     }
    else
    {
      reversed_pixel_address = led_strip.pixel_address;
    }
    
    // use io_dip[2:0]  to encode 12 unique pixel values at most (24 bits)
    case (state.q){
      state.IDLE:
             if (latch_button_edge.out){
                temp_encoding.d = 16x{c{io_dip[2], io_dip[2], io_dip[1], io_dip[0]}};
                state.d = state.LOAD;
              }
             if (clear_button_edge.out){
                 led_strip.clear = 1;
              }
      
      state.LOAD:
            // wait until we are at RESET state before we decide to refresh the screen
            if (led_strip.reset){
                // tell the LED to update the pixels
                led_strip.update = 1;
                led_encoding.d = temp_encoding.q;
                state.d = state.IDLE;
            }
    }
    
    // led_strip.pixel_address will vary between 0000 to 1100 
    // address 0 --> encoding bit 1:0 
    // address 1 --> encoding bit 3:2 
    // address 2 --> encoding bit 5
    // address N --> encoding bit N*2+1:N*2
    
    // TEST
    if (matrix_used.q){ // if we decide to flip the leftmost bit, we reverse every other row
      // reverse every other row
      encoded_pixel_address = reversed_pixel_address * 2;
    }
    else{
      // don't reverse every other row 
      encoded_pixel_address = led_strip.pixel_address * 2;
    }
    
 
    // get current color encoding for this pixel
    for (index=0; index<$clog2(ENCODING_AMOUNT); index++){
       current_color_encoding[index] = led_encoding.q[encoded_pixel_address+index];
    }
    
    // based on the encoding extracted from dff led_encoding, we get the 24bit color value
    led_strip.color = LEDCOLOR[current_color_encoding]; 
    outled=led_strip.led;
    
    // debug 
    // show the current LED color
    io_led[0] = led_encoding.q[7:0];
    io_led[1] = led_encoding.q[15:8];
    io_led[2] = led_encoding.q[23:16];
    led[0] = reverse.q;
    led[1] = matrix_used.q; 
    led[7:2] = led_strip.pixel_address;
    

    
  }
}